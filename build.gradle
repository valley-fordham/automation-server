plugins {
    id 'java-library'
}

group 'com.glenfordham'
version '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

sourceSets.main.java.srcDirs = ['src/main/java/']
// Configuration schema and Log4j2 XML resources
sourceSets.main.resources.srcDirs = [ "src/main/resources" ]
sourceSets.main.resources.includes = [ "**/*.xml", "**/*.xsd" ]

compileJava.dependsOn('buildJavaFromSchema')

jar {
    doFirst {
        manifest {
            attributes(
                    'Main-Class': 'com.glenfordham.webserver.Application',
                    'Implementation-Title': 'Web Server',
                    'Implementation-Version': getArchiveVersion()
            )
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    final String tomcatVersion = '9.0.36'

    testImplementation group: 'junit', name: 'junit', version: '4.12'

    implementation('com.sun.mail:javax.mail:1.6.2')
    implementation('commons-cli:commons-cli:1.4')
    implementation('javax.activation:activation:1.1')
    implementation('javax.xml.bind:jaxb-api:2.3.1')
    implementation('org.apache.logging.log4j:log4j-core:2.13.3')
    implementation('org.apache.tomcat.embed:tomcat-embed-core:'+tomcatVersion)
    implementation('org.apache.tomcat.embed:tomcat-embed-jasper:'+tomcatVersion)
    implementation('org.apache.tomcat:tomcat-jasper:'+tomcatVersion)
    implementation('org.apache.tomcat:tomcat-jasper-el:'+tomcatVersion)
    implementation('org.apache.tomcat:tomcat-jsp-api:'+tomcatVersion)
    implementation('org.glassfish.jaxb:jaxb-runtime:2.3.3')
}

// Generate JAXB objects from schema at build-time
task buildJavaFromSchema(type:Exec) {
    workingDir '.\\'
    commandLine 'cmd', '/c', '.\\tools\\jaxb-ri\\bin\\xjc.bat -b .\\src\\main\\resources\\com\\glenfordham\\webserver\\automation\\config\\bindings.xml -d .\\src\\main\\java -p com.glenfordham.webserver.automation.jaxb .\\src\\main\\resources\\com\\glenfordham\\webserver\\automation\\config\\config.xsd -extension'
    errorOutput = new ByteArrayOutputStream()
    ext.output = {
        return errorOutput.toString()
    }
}