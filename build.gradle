plugins {
    id 'java-library'
    id "com.github.johnrengelman.shadow" version "6.0.0"
}

group 'com.glenfordham'
version '1.0.'+gitCommitNumber()

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

sourceSets.main.java.srcDirs = ['src/main/java/']
// Configuration schema and Log4j2 XML resources
sourceSets.main.resources.srcDirs = [ "src/main/resources" ]
sourceSets.main.resources.includes = [ "**/*.xml", "**/*.xsd" ]

final String packagePath = "$buildDir\\package\\"
final String buildJrePath = packagePath + "$project.name-$version\\"
final String libsPath = "$buildDir\\libs\\"
final String srcPath = sourceSets.main.java.srcDirs[0].toPath().toString()
final String configPath = sourceSets.main.resources.srcDirs[0].toPath().toString() + "\\com\\glenfordham\\webserver\\automation\\config\\"
final String toolsPath = ".\\tools\\"

compileJava.dependsOn('buildJavaFromSchema')

jar {
    manifest {
        attributes(
                'Main-Class': 'com.glenfordham.webserver.Application',
                'Implementation-Title': project.name,
                // Use 'version' so Shadow Jar gets version properly
                'Implementation-Version': version,
        )
    }
}

repositories {
    mavenCentral()
}

dependencies {
    final String tomcatVersion = '9.0.36'

    testImplementation 'junit:junit:4.12'

    implementation 'com.sun.mail:javax.mail:1.6.2'
    implementation 'commons-cli:commons-cli:1.4'
    implementation 'commons-io:commons-io:2.7'
    implementation 'jakarta.activation:jakarta.activation-api:2.0.0-RC3'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:3.0.0-RC3'
    implementation 'org.apache.logging.log4j:log4j-core:2.13.3'
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:'+tomcatVersion
    implementation 'org.apache.tomcat.embed:tomcat-embed-jasper:'+tomcatVersion
    implementation 'org.apache.tomcat:tomcat-jasper:'+tomcatVersion
    implementation 'org.apache.tomcat:tomcat-jasper-el:'+tomcatVersion
    implementation 'org.apache.tomcat:tomcat-jsp-api:'+tomcatVersion
    implementation 'org.glassfish.jaxb:jaxb-runtime:3.0.0-M4'
}

// Generate JAXB objects from schema at build-time
task buildJavaFromSchema(type:Exec) {
    workingDir '.\\'
    commandLine 'cmd', '/c', toolsPath + 'jaxb-ri\\bin\\xjc.bat -b ' + configPath + 'bindings.xml -d ' + srcPath + ' -p com.glenfordham.webserver.automation.jaxb ' + configPath + 'config.xsd -extension'
}

// Output all dependency jars to a folder
task copyToLib(type: Copy) {
    from configurations.compileClasspath
    into libsPath
}

// Analyse project and retrieve modules used
task getModuleList(dependsOn:["copyToLib","jar"],type:Exec) {
    workingDir '.\\'
    commandLine 'cmd', '/c', 'jdeps --print-module-deps --multi-release 11 ' + libsPath + '*'
    standardOutput = new ByteArrayOutputStream()
    // Allows other tasks to get output of this task
    ext.out = {
        // Remove unwanted line breaks
        return standardOutput.toString().replace("\r\n", "")
    }
}

// Build Windows runtime files with jlink
task buildWinJre(dependsOn:["getModuleList","cleanWinJre"],type:Exec) {
    doFirst {
        workingDir '.\\'
        commandLine 'cmd', '/c', 'jlink --add-modules ' + (String) tasks.getModuleList.out() + ' --output ' + buildJrePath
    }
}

// Delete existing JRE folder
task cleanWinJre(type:Delete) {
    doFirst {
        try {
            project.delete buildJrePath
        } catch (IOException ignore) {}
    }
}

// Copy FAT jar to JRE for packaging
task copyFatJar(dependsOn:"shadowJar",type:Copy) {
    from file(libsPath + project.name + "-" + version + "-all.jar")
    into file(buildJrePath)
}

// Make deploy package TODO: create runtime scripts to remove the need for "java -jar etc"
task makePackageWindows(dependsOn:["buildWinJre","copyFatJar"]) {
}

// Zip Windows JRE and fat jar
task zipPackageWindows(dependsOn:"makePackageWindows",type:Zip) {
    from buildJrePath
    include '*'
    include '*/*' //to include contents of a folder present inside Reports directory
    archiveName project.name + "-" + version +'-win.zip'
    destinationDir(file(packagePath))
}

static def gitCommitNumber() {
    String commitNumber = "git rev-list --count HEAD --no-merges".execute().text.toString()
    if (commitNumber.trim().isEmpty()) {
        return 0
    } else {
        return commitNumber.trim().toInteger()
    }
}